plugins {
	id 'java'
	id "io.qameta.allure" version "2.10.0"
}

version 'unspecified'

repositories {
	mavenLocal()
	maven {
		url = uri('https://repo.maven.apache.org/maven2/')
	}
	mavenCentral()
}

ext {
	cucumberVersion = '7.7.0'
	junitJupiterapi = '5.9.0'
	jacksonDatabind = '2.9.5'
	apacheHttpClient = '4.5.13'
	allureMaven = '2.11.2'
	allureCucumber = '2.18.1'
	allureEnvironment = '1.0.0'
}

dependencies {

	implementation "io.qameta.allure:allure-maven:$allureMaven"
	implementation "io.qameta.allure:allure-cucumber7-jvm:$allureCucumber"
	implementation "com.github.automatedowl:allure-environment-writer:$allureEnvironment"

	testImplementation "io.cucumber:cucumber-java:$cucumberVersion"
	testImplementation "io.cucumber:cucumber-junit:$cucumberVersion"
	testImplementation "org.apache.httpcomponents:httpclient:$apacheHttpClient"
	testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterapi"
	testImplementation "com.fasterxml.jackson.core:jackson-databind:$jacksonDatabind"
}

configurations {
	cucumberRuntime {
		extendsFrom testImplementation
	}
}

def tags = findProperty('tags') == null ? 'not @Ignore' : "${findProperty('tags')} and not @Ignore"
test {
	systemProperty "cucumber.filter.tags", System.getProperty("cucumber.filter.tags")
	systemProperty "cucumber.options", System.properties.getProperty("cucumber.options")
	useJUnitPlatform()
}


task cucumber() {
	dependsOn assemble, testClasses
	doLast {
		javaexec {
			main = "io.cucumber.core.cli.Main"
			classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
			args = [
				'--plugin',
				'io.qameta.allure.cucumber7jvm.AllureCucumber7Jvm',
				'--plugin',
				'pretty',
				'--tags',
				"${tags}",
				'--glue',
				'gov/uk/di/ipv/cri/common/api/stepDefinitions',
				'src/test/resources'
			]
		}
	}
}

gradle.startParameter.excludedTaskNames.add(':integration-tests:test')
